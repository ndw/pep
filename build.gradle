plugins {
  id "java"
  id "maven-publish"
  id "signing"
  id 'com.github.gmazzo.buildconfig' version "2.0.2"
}

repositories {
  mavenLocal()
  mavenCentral()
}

dependencies {
  implementation (
    [group: "commons-cli", name: "commons-cli", version: "1.2"],
    [group: 'junit', name: 'junit', version: '4.13.2'],
  )
}

buildConfig {
  packageName("org.xproc")
  buildConfigField('String', 'TITLE', "\"${pepTitle}\"")
  buildConfigField('String', 'VERSION', "\"${pepVersion}\"")
}

jar {
  archiveBaseName = "pep-${pepVersion}"
  manifest {
    attributes "Built-By": "Norman Walsh"
    attributes "Implementation-Vendor": "Norman Walsh"
    attributes "Implementation-Title": pepTitle
    attributes "Implementation-Version": pepVersion
  }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: ["generateBuildConfig"]) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation"
}

// ============================================================

signing {
  sign publishing.publications
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      pom {
        name = 'Pep'
        packaging = 'jar'
        description = 'Pep is an Earley parser'
        url = 'https://github.com/xproc/pep'

        scm {
          url = 'scm:git@github.com:xproc/pep.git'
          connection = 'scm:git@github.com:xproc/pep.git'
          developerConnection = 'scm:git@github.com:xproc/pep.git'
        }

        licenses {
          license {
            name = 'GNU Lesser General Public License, version 2.1'
            url = 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.en.html'
            distribution = 'repo'
          }
        }

        developers {
          developer {
            id = 'ndw'
            name = 'Norman Walsh'
          }
        }
      }

      groupId = "org.xproc"
      artifactId = "pep"
      version = pepVersion
      from components.java
      artifact javadocJar
      artifact sourcesJar
    }
  }

  repositories {
    maven {
      url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
      credentials {
        username = findProperty("sonatypeUsername") ?: ""
        password = findProperty("sonatypePassword") ?: ""
      }
    }
  }
}

// ============================================================

task helloWorld() {
  doLast {
    println("Hello, world.")
  }
}
